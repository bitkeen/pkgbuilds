# Maintainer: Felix Wiegand <koffeinflummi@gmail.com>

_pkgname=htop
commit=402e46bb82964366746b86d77eb5afa69c279539
pkgname=${_pkgname}-vim
pkgver=2.2.0.${commit}
pkgrel=1
pkgdesc="Interactive text-mode process viewer. Patched for vim keybindings"
url="https://github.com/htop-dev/${_pkgname}"
license=('GPL')
arch=('i686' 'x86_64' 'armv7h')
depends=('ncurses')
makedepends=('git' 'python2')
optdepends=('lsof: list open files for running process'
            'strace: attach to running process')
provides=('htop')
conflicts=('htop' 'htop-git')
options=('!emptydirs')
source=("htop.tar.gz"::"${url}/archive/${commit}.tar.gz"
        'vim-keybindings.patch')
sha256sums=('8cdf21790924208db0bfa974812d4577a46259534112c75d32efdd9e94049be8'
            'ab1df29dc5a73d8e723137ffc767d2592e6024b6a6ffeb13e7c967f04008a255')

prepare() {
    cd "${srcdir}/${_pkgname}-${commit}"

    for _p in ${srcdir}/*.patch
    do
        msg2 "Applying $_p"
        patch -p1 -i $_p
    done

    ./autogen.sh

    sed -i 's|ncursesw/curses.h|curses.h|' RichString.[ch] configure
    sed -i 's|python|python2|' scripts/MakeHeader.py

    ./configure \
        CFLAGS="-O2 -fno-common" \
        --prefix=/usr \
        --enable-unicode \
        --enable-openvz \
        --enable-vserver \
        --enable-cgroup
}

build() {
    cd "${srcdir}/${_pkgname}-${commit}"
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${commit}"
    make DESTDIR="${pkgdir}" install
}
